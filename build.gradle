plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.2.0'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.0'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'

	id 'maven-publish'
	id 'signing'
	id 'org.jetbrains.dokka' version '1.9.20'
	id 'com.gradleup.nmcp' version '0.0.8'
	id 'jacoco'
}

group = 'org.drivine'
version = '0.0.17'

base {
	archivesName = 'drivine4j'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
}

repositories {
	mavenCentral()
}

// Configuration for Neo4j plugins (downloaded to .m2 for testcontainer mounting)
configurations {
	neo4jPlugins
}

dependencies {
	// Neo4j plugins for test containers - these are server-side plugins, not Java dependencies
	// They get mounted into the Neo4j container's /plugins directory
	neo4jPlugins 'org.neo4j.procedure:apoc-extended:5.26.0'
	implementation 'org.neo4j.driver:neo4j-java-driver:5.28.9'

	// Boot dependencies are useful in tests and samples; publishing will use plain JAR
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.aspectj:aspectjweaver:1.9.7'

	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// Test support - needed for @Rollback and TestExecutionListener
	implementation 'org.springframework:spring-test'
	implementation 'org.springframework:spring-tx'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// TestContainers
	// Testcontainers - provided as api dependency so DrivineTestContainer and
	// @EnableDrivineTestConfig work out-of-the-box for users without additional dependencies
	api 'org.testcontainers:neo4j:1.19.3'

	testImplementation 'org.testcontainers:testcontainers:1.19.3'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.3'

	// Spring Boot Starter for tests
	testImplementation project(':drivine4j-spring-boot-starter')

	// Fix Dokka Jackson compatibility issue
	dokkaPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:1.9.20'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict', '-Xcontext-parameters'
	}
}

// Use JUnit 5
tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // Generate coverage report after tests
}

// JaCoCo configuration for code coverage
jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

// Ensure we publish a normal library JAR, not the Spring Boot fat jar
tasks.named('bootJar') {
	enabled = false
}
tasks.named('jar') {
	enabled = true
	archiveClassifier.set('')  // Remove '-plain' classifier to make it the main JAR
}

// Task to download Neo4j plugins to Gradle cache (for DrivineTestContainer)
tasks.register('downloadNeo4jPlugins') {
	description = 'Downloads Neo4j plugins (APOC, GDS) to Gradle cache for test containers'
	doLast {
		configurations.neo4jPlugins.resolve().each { file ->
			println "Downloaded: ${file.name} -> ${file.absolutePath}"
		}
	}
}

// Ensure plugins are downloaded before tests run
tasks.named('test') {
	dependsOn 'downloadNeo4jPlugins'
}

// --- Dokka-based javadoc jar for Kotlin ---
// Temporary workaround: create empty javadoc jar to avoid Dokka/Jackson compatibility issue
tasks.register('javadocJar', Jar) {
	archiveClassifier = 'javadoc'
	// Empty for now - can enable Dokka later when compatibility is resolved
	// dependsOn tasks.named('dokkaHtml')
	// from tasks.named('dokkaHtml').get().outputDirectory
}

// --- Publishing configuration ---
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			// Explicit coords (optional if group/version/base.archivesName are set)
			groupId = project.group
			artifactId = project.extensions.getByType(BasePluginExtension).archivesName.get()
			version = project.version

			// Attach javadoc jar (sources jar is already included via components.java + withSourcesJar())
			artifact tasks.named('javadocJar')

			pom {
				name = 'drivine4j'
				description = 'Drivine for JVM â€” Neo4j/Spring-oriented utilities'
				url = 'https://github.com/liberation-data/drivine4j'

				licenses {
					license {
						name = 'Apache License 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
						distribution = 'repo'
					}
				}
				developers {
					developer {
						id = 'jasperblues'
						name = 'Jasper Blues'
						email = 'jasper@liberation-data.com'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/liberation-data/drivine4j.git'
					developerConnection = 'scm:git:ssh://git@github.com/liberation-data/drivine4j.git'
					url = 'https://github.com/liberation-data/drivine4j'
				}
			}
		}
	}

	// No longer need explicit repositories - nmcp plugin handles Central Portal publishing
}

// --- Signing (required for non-SNAPSHOT releases to Maven Central) ---
// Configure signing to be optional for local builds
def shouldSign = project.hasProperty('sign') || System.getenv('SIGN') == 'true'

if (shouldSign) {
	signing {
		// Only require signing when explicitly publishing to Maven Central
		required { gradle.taskGraph.hasTask('publish') && !version.endsWith('SNAPSHOT') }

		def signingKey = findProperty('signingKey') ?: System.getenv('SIGNING_KEY')
		def signingPassword = findProperty('signingPassword') ?: System.getenv('SIGNING_PASSWORD')

		if (signingKey != null && signingKey.contains('BEGIN PGP')) {
			// Full ASCII-armored key provided - use in-memory signing
			useInMemoryPgpKeys(signingKey, signingPassword)
		} else {
			// Key ID provided or default - use GPG agent
			useGpgCmd()
		}

		sign publishing.publications.mavenJava
	}
}

// --- Central Publishing Portal (new Maven Central) ---
nmcp {
	publishAllPublications {
		username = findProperty('sonatypeUsername') ?: System.getenv('SONATYPE_USERNAME')
		password = findProperty('sonatypePassword') ?: System.getenv('SONATYPE_PASSWORD')
		// Automatically publish (set to false to manually release from web UI)
		publicationType = "AUTOMATIC"
	}
}
